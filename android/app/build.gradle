plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.zilpaymobile"
    ndkVersion = "29.0.13113456"
    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "com.zilpaymobile"
        minSdk = 24
        targetSdk = 36
        targetSdkVersion 36
        multiDexEnabled true

        versionCode = flutter.versionCode
        versionName = flutter.versionName

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    splits {
        abi {
            enable true
            reset()
            universalApk false
        }
    }

    signingConfigs {
        release {
            if (keystoreProperties.containsKey('ZILPAY_UPLOAD_STORE_FILE')) {
                storeFile file(keystoreProperties['ZILPAY_UPLOAD_STORE_FILE'])
                storePassword keystoreProperties['ZILPAY_UPLOAD_STORE_PASSWORD']
                keyAlias keystoreProperties['ZILPAY_UPLOAD_KEY_ALIAS']
                keyPassword keystoreProperties['ZILPAY_UPLOAD_KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            signingConfig keystoreProperties.containsKey('ZILPAY_UPLOAD_STORE_FILE')
                ? signingConfigs.release
                : signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source = "../.."
}
dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.biometric:biometric:1.2.0-alpha05'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
}
